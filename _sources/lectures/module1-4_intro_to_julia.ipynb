{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9b7de353",
   "metadata": {},
   "source": [
    "# 4) Introduction to programming in Julia\n",
    "\n",
    "Last time:\n",
    "- Introduction to PDEs\n",
    "\n",
    "Today: \n",
    "1. Using Julia  \n",
    "2. Errors   \n",
    "    2.1. Plotting    \n",
    "3. Machine Epsilon  \n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d889396b",
   "metadata": {},
   "source": [
    "## 1. Julia\n",
    "\n",
    "To recap from first class, Julia is a relatively new programming language. Think of it as MATLAB done right, open source, and fast. It's nominally general-purpose, but mostly for numerical/scientific/statistical computing. There are great [learning resources](https://julialang.org/learning/). We'll introduce concepts and language features as we go.\n",
    "\n",
    "It is supported in Jupyter Notebooks.\n",
    "\n",
    "From your terminal, start a Julia session with\n",
    "```\n",
    "julia\n",
    "```\n",
    "\n",
    "To add the `IJulia` package to your environment, then type:\n",
    "```julia\n",
    "]add IJulia\n",
    "```\n",
    "\n",
    "go back to your Julia REPL using backspace and type\n",
    "\n",
    "```julia\n",
    "julia> using IJulia\n",
    "```\n",
    "\n",
    "and then\n",
    "```julia\n",
    "julia> notebook()\n",
    "```\n",
    "\n",
    "to run the notebook.\n",
    "\n",
    "Let's see it with live demos:\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "62bfb909",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The last line of a cell is output by default\n",
    "x = 3\n",
    "y = 4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a9f9bdd0",
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"$x + $y = $(x + y)\") # string formatting/interpolation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2767beec",
   "metadata": {},
   "outputs": [],
   "source": [
    "4;  # trailing semicolon suppresses output"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b0a54d43",
   "metadata": {},
   "source": [
    "Julia has several _macros_, invoked by the `@` symbol. A useful one is the `@show` macro (which prints one or more expressions, and their results, to `stdout`, and returns the last result):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0e87596b",
   "metadata": {},
   "outputs": [],
   "source": [
    "@show x + y # here the entire expression is shown\n",
    "x * y # here only the return (computed) value is shown"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bf88f495",
   "metadata": {},
   "source": []
  },
  {
   "cell_type": "markdown",
   "id": "f46c5a2c",
   "metadata": {},
   "source": [
    "### 1.1. Numbers\n",
    "\n",
    ":::{tip}\n",
    "Check this very helpful [Julia documentation page](https://docs.julialang.org/en/v1/manual/integers-and-floating-point-numbers/).\n",
    ":::"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b5e5de3a",
   "metadata": {},
   "outputs": [],
   "source": [
    "3, 3.0, 3.0f0, big(3.0) # integers, double precision, single precision, and convert to a maximum precision representation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "143e6201",
   "metadata": {},
   "outputs": [],
   "source": [
    "typeof(3), typeof(3.0), typeof(3.0f0), typeof(big(3.0))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fbfe82eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "# automatic promotion\n",
    "@show 3 + 3.0\n",
    "@show 3.0 + 3.0f0\n",
    "@show 3 + 3.0f0;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a2927f79",
   "metadata": {},
   "outputs": [],
   "source": [
    "# floating and integer division\n",
    "@show 4 / 2\n",
    "@show -3 รท 2; # type `\\div` and press TAB"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4016cfab",
   "metadata": {},
   "source": [
    "### 1.2 Arrays"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "13092428",
   "metadata": {},
   "outputs": [],
   "source": [
    "[1, 2, 3]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27f90456",
   "metadata": {},
   "outputs": [],
   "source": [
    "# promotion rules for arrays are similar to arithmetic\n",
    "[1,2,3.] + [1,2,3]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f6b49204",
   "metadata": {},
   "outputs": [],
   "source": [
    "x = [10., 20, 30]\n",
    "x[2] # one-based indexing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "902150c8",
   "metadata": {},
   "outputs": [],
   "source": [
    "x[2] = 3.5"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c04fea81",
   "metadata": {},
   "source": [
    "Compare with the Python notation:\n",
    "\n",
    "```python\n",
    "A = np.array([[10, 20, 30], [40, 50, 60]])\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d08ac602",
   "metadata": {},
   "source": [
    "### 1.3 Functions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b3e7d6b4",
   "metadata": {},
   "outputs": [],
   "source": [
    "# define a function called 'f' that takes three arguments, one of which has a default value\n",
    "function f(x, y; z=3)\n",
    "    sqrt(x*x + y*y) + z\n",
    "end\n",
    "\n",
    "# define another function also called 'f' that only takes two arguments\n",
    "function f(x, y)\n",
    "    sqrt(x*x + y*y)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0dfa8cd3",
   "metadata": {},
   "source": [
    "What is the difference between a **method** and a **function** in Julia? Check this [resource](https://docs.julialang.org/en/v1/manual/methods/)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6c8b1d51",
   "metadata": {},
   "outputs": [],
   "source": [
    "# if I invoke f this way, which one will the Julia compiler use?\n",
    "f(3, 4, z=5)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bde33377",
   "metadata": {},
   "outputs": [],
   "source": [
    "# if instead I invoke f this way, which one will the Julia compiler use?\n",
    "f(2, 3)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c209f46b",
   "metadata": {},
   "source": [
    "There are also compact \"assignment form\" function definitions:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a58e47ec",
   "metadata": {},
   "outputs": [],
   "source": [
    "g(x, y) = sqrt(x^2 + y^2)\n",
    "g(3, 4)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cf82689a",
   "metadata": {},
   "source": [
    "In the assignment form above, the body of the function must be a single expression, although it can be a compound expression."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c768ca14",
   "metadata": {},
   "source": [
    "We have just seen an example of one of the most powerful features of Julia: **dynamic [(multiple) dispatch](https://en.wikipedia.org/wiki/Multiple_dispatch)**: \n",
    "\n",
    "- Functions can have multiple definitions as long each definition restricts the type of the parameters differently. It is the type of the parameters that define which \"definition\" (or \"method\" in Julia terminology) will be called. \n",
    "\n",
    "- This is the way Julia mimics [polymorphism](https://en.wikipedia.org/wiki/Polymorphism_(computer_science)) that some compiled languages (e.g., C++) have."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d8633b46",
   "metadata": {},
   "source": [
    "**Anonymous functions** are usually functions so short-lived that they do not need a name. This is done with an arrow notation. Example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6381b69b",
   "metadata": {},
   "outputs": [],
   "source": [
    "((x, y) -> sqrt(x^2 + y^2))(3, 4)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "858469b4",
   "metadata": {},
   "source": [
    "### 1.4 Ranges and loops"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc79964d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# ranges\n",
    "1:50000000"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "671fc342",
   "metadata": {},
   "outputs": [],
   "source": [
    "collect(1:5) # creates a vector from a range of values"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a8bf1c6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "x = 0\n",
    "# example of a for loop\n",
    "for n in 1:50000000  \n",
    "    x += 1/n^2\n",
    "end\n",
    "@show x\n",
    "x - pi^2/6 # Basel problem (solved by Euler; generalized by Riemann's zeta function)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f59edefc",
   "metadata": {},
   "outputs": [],
   "source": [
    "# list comprehensions\n",
    "sum([1/n^2 for n in 1:1000])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "79ab7aa6",
   "metadata": {},
   "source": [
    "## 2. Errors\n",
    "\n",
    "There are several sources of error in computation:\n",
    "\n",
    "* roundoff (a consequence of how computers represent numbers)\n",
    "* conditioning (a property of the problem we're solving)\n",
    "* stability (a property of the algorithm we're using)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5c8f7449",
   "metadata": {},
   "source": [
    "\n",
    "### 2.1 Roundoff errors and Floating Point Arithmetic (FPA)\n",
    "\n",
    "* If $\\circ$ is an operator that rounds a real number $x \\in \\mathbb{R}$ to the nearest _floating-point number_ $\\tilde{x}$:\n",
    "\n",
    "$$ \\tilde{x} := \\circ x,$$\n",
    "\n",
    "* Then the relative error introduced by this representation is\n",
    "\n",
    "$$ \\delta x = \\frac{\\tilde{x} - x}{x}, $$\n",
    "\n",
    "* Rearranged:\n",
    "\n",
    "$$ \\tilde{x} = (1 + \\delta x)x. $$\n",
    "\n",
    "* What is a guaranteed _upper bound_ on $\\delta x$?\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "66b5f42f",
   "metadata": {},
   "source": [
    "* The IEEE (Institute of Electrical and Electronics Engineers) stadard guarantees that \n",
    "\n",
    "$$\n",
    "|\\delta x| < \\mu_M = \\frac{1}{2} \\varepsilon_M\n",
    "$$,\n",
    "\n",
    "where $\\varepsilon_M$ is _machine precision_."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bccc2664",
   "metadata": {},
   "source": [
    "**Question**: What is $\\varepsilon_M$ for single- and double-precision arithmetic?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "62581569",
   "metadata": {},
   "outputs": [],
   "source": [
    "eps(Float64)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "633e4050",
   "metadata": {},
   "outputs": [],
   "source": [
    "eps(Float32)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a6aa75a1",
   "metadata": {},
   "source": [
    "It's around $10^{-7}$ for single-precision, and $10^{-16}$ for double-precision floating point numbers. \n",
    "* What does this mean for the number of digits?\n",
    "* Why would anyone choose single precision?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "16ac6d13",
   "metadata": {},
   "outputs": [],
   "source": [
    "x = 1e-16\n",
    "@show 1+x # :-("
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f589ee33",
   "metadata": {},
   "source": [
    "## 3. Plotting\n",
    "\n",
    "There are several plotting libraries in Julia. [Plots.jl](https://docs.juliaplots.org/stable/) is a basic package for 1D plots and simple planar maps. [Makie.jl](https://docs.makie.org/stable/) is a much more sophisticated package with richer 2D/3D visualizations, dynamic sliders, animations, etc.\n",
    "\n",
    "We will use Plots.jl in class. \n",
    "\n",
    "Before being able to use it, you have to add it to your environment."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "21f843e5",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "Pkg.add(\"Plots\") # adds the Plots.jl package to your environment\n",
    "using Plots # once added, you can use it\n",
    "default(linewidth=4, legendfontsize=12, xtickfontsize=12, ytickfontsize=12) # sets some defaults for embelishments"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b6014b77",
   "metadata": {},
   "outputs": [],
   "source": [
    "x = range(0, 10, length=100)\n",
    "y = sin.(x)\n",
    "plot(x, y, label = \"f(x)=sin(x)\", xlabel = \"x\", ylabel = \"y\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
